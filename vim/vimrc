" .vimrc

" {{{ vim-plug

call g:plug#begin('~/.vim/plugged')

" Better status line
Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'

" ga for character descriptions
Plug 'tpope/vim-characterize'

" :Gdiff, :Gcommit, etc.
Plug 'tpope/vim-fugitive'

" Markdown syntax
Plug 'tpope/vim-markdown'

" Add support for github-style fenced codeblocks in markdown
Plug 'jtratner/vim-flavored-markdown'

" Python syntax folding
Plug 'tmhedberg/SimpylFold'

" Syntax, linting, etc.
Plug 'w0rp/ale'

" Clojure syntax
Plug 'guns/vim-clojure-static'

" lein support
Plug 'tpope/vim-salve'

" Solarized theme
Plug 'altercation/vim-colors-solarized'

" Javascript syntax
Plug 'othree/yajs.vim', { 'for': 'javascript'}

" JSON syntax highlighting
Plug 'elzr/vim-json'

" Comment with gc
Plug 'tpope/vim-commentary'

" Amazing mappings
Plug 'tpope/vim-unimpaired'

" Repeat things
Plug 'tpope/vim-repeat'

" JSON pretty printing with gqaj
Plug 'tpope/vim-jdaddy'

" git runtime files - highlight git config, commit, etc.
Plug 'tpope/vim-git'

" Inline git status
Plug 'airblade/vim-gitgutter'

" Coffeescript
Plug 'kchmck/vim-coffee-script'

" Open current Markdown buffer in Marked2
Plug 'itspriddle/vim-marked'

" Better rainbow parens
Plug 'luochen1990/rainbow'

" iTerm2 and tmux integration
Plug 'sjl/vitality.vim'

" Lint vimscript with vint; requires vint: pip install vim-vint
Plug 'todesking/vint-syntastic'

" netrw improvements
Plug 'tpope/vim-vinegar'

" argument manipulation
Plug 'PeterRincker/vim-argumentative'

" manipulate delimiters
Plug 'tpope/vim-surround'

" even better (and consistent) fuzzy finding
Plug 'junegunn/fzf', { 'do': './install --bin --no-key-bindings --no-completion --no-update-rc' }
Plug 'junegunn/fzf.vim'

" async build tasks
Plug 'tpope/vim-dispatch'

" github integration
Plug 'tpope/vim-rhubarb'

" Dash.app integration (OS X only)
Plug 'rizzatti/dash.vim'

" Async interactive scratchpad
Plug 'metakirby5/codi.vim'

" GraphQL syntax
Plug 'jparise/vim-graphql'

" CoC.nvim LSP
Plug 'neoclide/coc.nvim', {'tag': '*', 'do': { -> coc#util#install()}}

" Prose
Plug 'junegunn/goyo.vim'
Plug 'junegunn/limelight.vim'

call g:plug#end()

filetype plugin indent on
syntax enable

" }}}
" {{{ Setup

" Set the terminal font encoding
set encoding=utf-8
scriptencoding utf-8
set termencoding=utf-8

" Manage multiple buffers
set hidden

" Map leader to <space>
let g:mapleader = ' '
let g:maplocalleader = ' '

" More frequent updates for, e.g. signs.
set updatetime=750

" }}}
" {{{ Ack
" Skip normal config, show filenames, no color, one result per line, show column numbers, smart case
let g:ackprg='ack --noenv -H --nocolor --nogroup --column --smart-case --after=0 --before=0'

" }}}
" {{{ Airline
let g:airline_theme = 'solarized'

" No separators between sections
let g:airline_left_sep = ''
let g:airline_right_sep = ''

if !exists('g:airline_symbols')
    let g:airline_symbols = {}
endif
let g:airline_symbols.whitespace = 'Ξ'
let g:airline_symbols.readonly = '∅'

" Modified theme
" * status same color as tab line
" * inactive bg different than status and ruler
function! g:AirlineThemeModified(palette)
    if g:airline_theme ==? 'solarized'
        let a:palette.normal_modified.airline_c =  ['#eee8d5', '#b58900', 254, 136, 'bold']
        let a:palette.insert_modified.airline_c =  ['#eee8d5', '#b58900', 254, 136, 'bold']
        let a:palette.visual_modified.airline_c =  ['#eee8d5', '#b58900', 254, 136, 'bold']
        let a:palette.inactive_modified.airline_c =['#b58900', '', 136, '', 'bold']

        for l:colors in values(a:palette.inactive)
            let l:colors[2] = 241
            let l:colors[3] = 235
        endfor
    endif
endfunction
let g:airline_theme_patch_func = 'g:AirlineThemeModified'

" Enable buffers and numbers in the tab bar
let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#tabline#buffer_nr_show = 0
let g:airline#extensions#tabline#buffer_min_count = 2
let g:airline#extensions#tabline#formatter = 'unique_tail_improved'
let g:airline#extensions#tabline#buffers_label = ''

" No need for mode (or crypt, paste, iminsert, etc.) because of showmode
" No need for file encoding
let g:airline#extensions#default#layout = [
    \ [ 'b', 'c' ],
    \ [ 'x', 'z', 'error', 'warning']
\ ]

" Enable ALE status
let g:airline#extensions#ale#enabled = 1
" }}}
" {{{ ALE
" Be explicit about languages I care about
let g:ale_linters = {
\   'javascript': ['eslint'],
\   'python': ['flake8'],
\   'vim': ['vint']
\}

" Ignore specific flake8 errors and warnings
let g:ale_python_flake8_options = '--ignore="E501,E302,E261,E701,E241,E126,E127,E128,E121,W801"'

" Better sign symbols
let g:ale_sign_error = '✗'
let g:ale_sign_warning = '!'
let g:ale_sign_style_error = '☡'
let g:ale_sign_style_warning = '¡'

" Always show the sign column
let g:ale_sign_column_always = 1

nmap <leader>ln <Plug>(ale_next_wrap)
nmap <leader>lp <Plug>(ale_previous_wrap)
" }}}
" {{{ Autocommands
if has('autocmd')
    " Clear existing autocommands before defining them, in case this file is loaded again
    autocmd!

    augroup last_cursor
        autocmd!

        " Jump to last cursor position unless it's invalid or in an event handler or
        " a git commit
        autocmd BufReadPost *
          \ if &filetype !~ '^git\c' && line("'\"") > 0 && line("'\"") <= line('$') |
          \   exe 'normal! g`"' |
          \ endif
    augroup END

    augroup cursor_highlight
        autocmd!

        " Only show cursorline in active windows
        autocmd WinLeave * set nocursorline colorcolumn=""
        autocmd WinEnter * set cursorline colorcolumn=+1
    augroup END

    augroup markdown
        autocmd!

        " Automatically use github-flavored markdown
        autocmd BufNewFile,BufRead *.md,*.markdown setlocal filetype=ghmarkdown

        autocmd User GoyoEnter Limelight
        autocmd User GoyoLeave Limelight!
        autocmd FileType markdown Goyo 110
    augroup END

    augroup golang
        autocmd!

        autocmd FileType go setlocal noexpandtab
    augroup END
endif

" }}}
" {{{ Backups
" Skip backup entirely. It's annoying.
set nobackup
if has('writebackup')
    set nowritebackup
endif

" }}}
" {{{ Commands
" Note that these options don't really matter when using vim-powerline.
if has('cmdline_info')
    " Show line and column information
    set ruler

    " Show command information in the status
    set showcmd

    " Show which mode we're in
    set showmode
endif

" }}}
" {{{ Diff
set diffopt+=vertical,algorithm:histogram,indent-heuristic
" }}}
" {{{ Folding
" Fold with triple curly braces, fill with spaces
if has('folding')
    set foldenable
    set foldmethod=marker
    set foldopen=hor,search,tag,undo
    set fillchars=diff:\ ,fold:\ ,vert:\
endif

" }}}
" {{{ Formatting
" Auto indent new lines
set autoindent

" Show matching braces
set showmatch

" Spacing and tabbing
" Use shiftwidth and tabstop smartly
set smarttab

" Spaces
set expandtab

" Tab width
set tabstop=4
set softtabstop=4
set shiftwidth=4

set textwidth=100

" Don't wrap text
set nowrap

" But if prose *is* wrapped with linebreak, make it look good
if has('linebreak')
    set breakindent
    let &showbreak = '» '
    set cpoptions+=n
end

" Highlight the first column after the text width
if exists('&colorcolumn')
    set colorcolumn=+1
endif

" From the Vim docs:
" t   Auto-wrap text using textwidth
"
" c   Auto-wrap comments using textwidth, inserting the current comment
"     leader automatically.
"
" r   Automatically insert the current comment leader after hitting
"     <Enter> in Insert mode.
"
" o   Automatically insert the current comment leader after hitting 'o' or
"     'O' in Normal mode.
"
" n   When formatting text, recognize numbered lists.  This actually uses
"     the 'formatlistpat' option, thus any kind of list can be used.  The
"     indent of the text after the number is used for the next line.  The
"     default is to find a number, optionally followed by '.', ':', ')',
"     ']' or '}'.  Note that 'autoindent' must be set too.  Doesn't work
"     well together with "2".
"     Example:
"         1. the first item
"            wraps
"         2. the second item
set formatoptions+=t,c,r,o,n

" Delete comment character when joining commented lines
if v:version > 703 || v:version == 703 && has('patch541')
  set formatoptions+=j
endif

" Use only 1 space after "." when joining lines instead of 2
set nojoinspaces

augroup vimrc
    autocmd!

    autocmd FileType xhtml,xml,html set textwidth=0
    autocmd FileType make set noexpandtab tabstop=8 softtabstop=8 shiftwidth=8
    autocmd FileType markdown set textwidth=0 wrapmargin=0 wrap linebreak
    autocmd FileType clojure set tabstop=2 softtabstop=2 shiftwidth=2
    autocmd FileType yaml set tabstop=2 softtabstop=2 shiftwidth=2
    autocmd FileType javascript set tabstop=2 softtabstop=2 shiftwidth=2
augroup END

" }}}
" {{{ Fugitive
nnoremap <leader>gb :Gblame<cr>
nnoremap <leader>gc :Gcommit<cr>
nnoremap <leader>gd :Gdiff<cr>
nnoremap <leader>gl :Glog<cr>
nnoremap <leader>gp :Git push<cr>
nnoremap <leader>gs :Git status -sb<cr>

" }}}
" {{{ fzf
let g:fzf_colors =
\ { 'fg':      ['fg', 'Normal'],
  \ 'bg':      ['bg', 'Normal'],
  \ 'hl':      ['fg', 'Comment'],
  \ 'fg+':     ['fg', 'CursorLine', 'CursorColumn', 'Normal'],
  \ 'bg+':     ['bg', 'CursorLine', 'CursorColumn'],
  \ 'hl+':     ['fg', 'Statement'],
  \ 'info':    ['fg', 'PreProc'],
  \ 'prompt':  ['fg', 'Conditional'],
  \ 'pointer': ['fg', 'Exception'],
  \ 'marker':  ['fg', 'Keyword'],
  \ 'spinner': ['fg', 'Label'],
  \ 'header':  ['fg', 'Comment'] }

nnoremap <silent> <leader><leader> :Files<cr>
nnoremap <silent> <leader><Enter> :Buffers<cr>

" Files command with preview window
command! -bang -nargs=? -complete=dir Files
  \ call fzf#vim#files(<q-args>, fzf#vim#with_preview(), <bang>0)

" Ripgrep with preview
" :Rg --type py PATTERN
" :Rg! --no-ignore PATTERN DIR
command! -bang -nargs=* Rg
  \ call fzf#vim#grep(
  \   'rg --vimgrep --color=always --smart-case '.<q-args>, 1,
  \   <bang>0 ? fzf#vim#with_preview('up:60%')
  \           : fzf#vim#with_preview('right:50%'),
  \   <bang>0)
nnoremap <silent> <leader>rg :Rg<space>

" }}}
" {{{ git-gutter
" Escape grep so we don't have color codes
let g:gitgutter_escape_grep = 1

" Ignore whitespace (we have other plugins for that)
let g:gitgutter_diff_args = '-w'

" Don't map any keys by default
let g:gitgutter_map_keys = 0

" Re-add some useful mappings
nmap ]h <Plug>(GitGutterNextHunk)
nmap [h <Plug>(GitGutterPrevHunk)
nmap <leader>ha <Plug>(GitGutterStageHunk)
nmap <leader>hr <Plug>(GitGutterRevertHunk)
nmap <leader>hd <Plug>(GitGutterPreviewHunk)

" Map hunk objects to ih and ah
omap ih <Plug>(GitGutterTextObjectInnerPending)
omap ah <Plug>(GitGutterTextObjectOuterPending)
xmap ih <Plug>(GitGutterTextObjectInnerVisual)
xmap ah <Plug>(GitGutterTextObjectOuterVisual)

" }}}
" {{{ History
" Command history
set history=1000

" }}}
" {{{ Key remaps
" Completion
inoremap <C-space> <C-X><C-P>

" Edit and source this file
nmap <silent> <leader>ev :split $MYVIMRC<CR>
nmap <silent> <leader>sv :source $MYVIMRC<CR>

" Annoyances

" No arrow keys
inoremap <up> <nop>
inoremap <down> <nop>
inoremap <left> <nop>
inoremap <right> <nop>
nnoremap <up> <nop>
nnoremap <down> <nop>
nnoremap <left> <nop>
nnoremap <right> <nop>

" Bind dumb uppercase commands to their useful lowercase equivalents
if has('user_commands')
    command! -bang E e<bang>
    command! -bang Q q<bang>
    command! -bang W w<bang>
    command! -bang QA qa<bang>
    command! -bang Qa qa<bang>
    command! -bang Wa wa<bang>
    command! -bang WA wa<bang>
    command! -bang Wq wq<bang>
    command! -bang WQ wq<bang>
endif

" Make Y act like other uppercase commands
nnoremap Y y$

" Disable going to Ex mode
noremap Q <nop>

" Remove annoying F1 help; use :help instead
noremap <F1> <nop>

" Shortcuts for diff
nnoremap <leader>dg :diffget<cr>
nnoremap <leader>dp :diffput<cr>
" }}}
" {{{ Line Numbers
" Show line numbers
set number
if exists('&relativenumber')
    set relativenumber
endif

" }}}
" {{{ List
" Toggle invisibles, hide by default
set nolist
set listchars=eol:¬,tab:▸·,trail:·,nbsp:_,extends:❯,precedes:❮
nnoremap <leader>l :set list!<CR>

" }}}
" {{{ Markdown
" Explicit syntax highlighting for fenced code blocks in markdown
let g:markdown_fenced_languages = ['javascript', 'html', 'python', 'bash=sh']

" }}}
" {{{ Matching
" Enable extended % matching
silent! runtime macros/matchit.vim

" }}}
" {{{ Messages
" Shorten a lot of notifications and suppress the splash screen
set shortmess+=aI

" }}}
" {{{ Prose
let g:limelight_conceal_ctermfg = 240
let g:limelight_paragraph_span = 1

let g:goyo_width = 110

" }}}
" {{{ Rainbow
let g:rainbow_active = 1

let g:rainbow_conf = {
    \   'guifgs': ['royalblue3', 'darkorange3', 'seagreen3', 'firebrick'],
    \   'ctermfgs': ['darkblue', 'darkyellow', 'darkmagenta', 'darkred', 'red', 'blue', 'yellow', 'magenta', 'cyan', 'lightblue', 'lightyellow', 'lightmagenta', 'lightcyan'],
    \   'operators': '_,_',
    \   'parentheses': ['start=/(/ end=/)/ fold', 'start=/\[/ end=/\]/ fold', 'start=/{/ end=/}/ fold'],
    \   'separately': {
    \       '*': {},
    \       'tex': {
    \           'parentheses': ['start=/(/ end=/)/', 'start=/\[/ end=/\]/'],
    \       },
    \       'lisp': {
    \           'guifgs': ['royalblue3', 'darkorange3', 'seagreen3', 'firebrick', 'darkorchid3'],
    \       },
    \       'vim': {
    \           'parentheses': ['start=/(/ end=/)/', 'start=/\[/ end=/\]/', 'start=/{/ end=/}/ fold', 'start=/(/ end=/)/ containedin=vimFuncBody', 'start=/\[/ end=/\]/ containedin=vimFuncBody', 'start=/{/ end=/}/ fold containedin=vimFuncBody'],
    \       },
    \       'html': {
    \           'parentheses': ['start=/\v\<((area|base|br|col|embed|hr|img|input|keygen|link|menuitem|meta|param|source|track|wbr)[ >])@!\z([-_:a-zA-Z0-9]+)(\s+[-_:a-zA-Z0-9]+(\=("[^"]*"|'."'".'[^'."'".']*'."'".'|[^ '."'".'"><=`]*))?)*\>/ end=#</\z1># fold'],
    \       },
    \       'css': 0,
    \   }
    \}
" }}}
" {{{ Ripgrep
set grepprg =rg\ --vimgrep\ $*

" }}}
" {{{ Scrolling
" Scrolling context; lines/columns of buffer while scrolling
set scrolloff=3
set sidescrolloff=5

" Scrolling speed
nnoremap <C-e> 5<C-e>
nnoremap <C-y> 5<C-y>

" }}}
" {{{ Search
if has('extra_search')
    " Highlight searches
    set hlsearch

    " Be smart about searching case-sensitively
    set smartcase
    set ignorecase

    " Search as you type
    set incsearch

    " Wrap around the file when searching
    set wrapscan
endif

" }}}
" {{{ Spelling
if has('spell')
    set spelllang=en_us
endif

" }}}
" {{{ Splits

" Intuitive split locations
set splitright
set splitbelow

augroup helpfiles
    autocmd!

    " Always open help in vertical split
    autocmd vimrc FileType help wincmd L
augroup END

" }}}
" {{{ Status line
" Always show the status line
set laststatus=2

" }}}
" {{{ Swap
" Turn swap off
set noswapfile
set updatecount=0

" }}}
" {{{ Terminal

" Better redrawing for large files
set ttyfast

" Disable the vbell
set visualbell t_vb=

" }}}
" {{{ Text Objects
" Line
xnoremap il g_o^
onoremap il :normal vil<CR>
xnoremap al $o0
onoremap al :normal val<CR>
" }}}
" {{{ Undo
" -- see http://amix.dk/blog/post/19548
if has('persistent_undo')
    set undodir=~/.vim/undodir
    set undofile

    " max changes that can be undone
    set undolevels=1000

    " max lines to save for undo on buffer reload
    set undoreload=10000
endif

" }}}
" {{{ Utility
" Respect vim modelines
set modeline

" Use the file's name in the title
set title

" Highlight the line the cursor is on
set cursorline

" Let backspace do what it's supposed to: allow backspace over indent, eol, and start of an insert
set backspace=indent,eol,start

" Reuse open buffers and tabs
set switchbuf=useopen,usetab

" }}}
" {{{ Wildmenu
if has('wildmenu')
    " Bash-like tab completion for commands
    set wildmenu
    set wildmode=longest:list
    set wildchar=<Tab>
    if has('wildignore')
        set wildignore+=*.a,*.o,*.pyc,.DS_Store
        set wildignore+=*.bmp,*.gif,*.ico,*.jpg,*.png
        set wildignore+=*~,*.swp,*.tmp
    endif
    if exists('&wildignorecase')
        set wildignorecase
    endif
endif

" }}}
" {{{ Windows
" Split windows below and right -- default is above and left, which can be disorienting
set splitbelow splitright

" Keep window sizes when opening and closing windows
set noequalalways

" }}}
" {{{ CoC.nvim
" Explicitly list installed extensions
let g:coc_global_extensions = ['coc-dictionary', 'coc-syntax', 'coc-tsserver', 'coc-json', 'coc-html', 'coc-css', 'coc-pyls']

" Confirm first item with <cr>
inoremap <silent><expr> <cr> pumvisible() ? coc#_select_confirm() : "\<C-g>u\<CR>"

" Rename symbol under cursor
nmap <leader>rn <Plug>(coc-rename)
nmap <leader>df <Plug>(coc-definition)

" {{{ Epilogue
" Turn syntax on after everything else
syntax on

" {{{ Colors
if $ITERM_PROFILE =~? 'light'
    set background=light
else
    set background=dark
endif

let g:solarized_termtrans = 1
let g:solarized_underline = 0
silent! colorscheme solarized

hi Folded term=standout ctermfg=3 ctermbg=NONE

highlight link SignColumn LineNr
highlight link GitGutterAdd DiffAdd
highlight link GitGutterChange DiffChange
highlight link GitGutterDelete DiffDelete
highlight link GitGutterChangeDelete DiffDelete

" }}}
" }}}
